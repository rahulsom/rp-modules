plugins {
    id("com.github.rahulsom.waena.root").version("0.3.1")
    id("com.github.rahulsom.waena.published").version("0.3.1").apply(false)
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'groovy'

    group = "com.github.rahulsom"

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'io.ratpack:ratpack-core:1.2.0'
        compileOnly 'io.ratpack:ratpack-guice:1.2.0'

        testImplementation 'io.ratpack:ratpack-test:1.2.0'
        testImplementation 'io.ratpack:ratpack-guice:1.2.0'
        testImplementation "org.codehaus.groovy:groovy-all:2.5.14"
        testImplementation "org.spockframework:spock-core:1.3-groovy-2.5"
        testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'

    }

}

abstract class RatpackAlignmentRule implements ComponentMetadataRule {
    void execute(ComponentMetadataContext ctx) {
        ctx.details.with {
            if (id.group.equals("io.ratpack")) {
                // declare that Jackson modules all belong to the Jackson virtual platform
                belongsTo("io.ratpack:ratpack-virtual-platform:${id.version}")
            }
        }
    }
}